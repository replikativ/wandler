;; Copyright (c) 2026 Christian Weilbach. All rights reserved.
;; Build script for wandler — build JAR, deploy. (Pure Clojure: no Java kernel, no javac.)

(ns build
  (:refer-clojure :exclude [test])
  (:require [clojure.tools.build.api :as b]
            [deps-deploy.deps-deploy :as dd]))

(def org "replikativ")
(def lib 'org.replikativ/wandler)
(def current-commit (b/git-process {:git-args "rev-parse HEAD"}))
(def version (format "0.1.%s" (b/git-count-revs nil)))
(def class-dir "target/classes")
(def basis (b/create-basis {:project "deps.edn"}))
(def jar-file (format "target/%s-%s.jar" (name lib) version))

(defn clean [_]
  (b/delete {:path "target"}))

(defn jar [_]
  (b/write-pom {:class-dir class-dir
                :lib lib
                :version version
                :basis basis
                :src-dirs ["src"]
                :scm {:url "https://github.com/replikativ/wandler"
                      :connection "scm:git:git://github.com/replikativ/wandler.git"
                      :developerConnection "scm:git:ssh://git@github.com/replikativ/wandler.git"
                      :tag (str "v" version)}
                :pom-data [[:description "A verified data-transformation runtime — write Clojure data pipelines, optimized by certified rewriting on the ansatz CIC kernel"]
                           [:url "https://github.com/replikativ/wandler"]
                           [:licenses
                            [:license
                             [:name "Apache License 2.0"]
                             [:url "https://www.apache.org/licenses/LICENSE-2.0"]]]
                           [:developers
                            [:developer
                             [:id "whilo"]
                             [:name "Christian Weilbach"]
                             [:email "ch_weil@topiq.es"]]]]})
  (b/copy-dir {:src-dirs ["src" "resources"]
               :target-dir class-dir})
  (b/jar {:class-dir class-dir
          :jar-file jar-file}))

(defn deploy
  "Deploy to Clojars. Set CLOJARS_USERNAME and CLOJARS_PASSWORD env vars."
  [_]
  (jar nil)
  (dd/deploy {:installer :remote :artifact jar-file
              :pom-file (b/pom-path {:lib lib :class-dir class-dir})}))

(defn fib [a b]
  (lazy-seq (cons a (fib b (+ a b)))))

(defn retry-with-fib-backoff [retries exec-fn test-fn]
  (loop [idle-times (take retries (fib 1 2))]
    (let [result (exec-fn)]
      (if (test-fn result)
        (do (println "Returned: " result)
            (if-let [sleep-ms (first idle-times)]
              (do (println "Retrying with remaining back-off times (in s): " idle-times)
                  (Thread/sleep (* 1000 sleep-ms))
                  (recur (rest idle-times)))
              result))
        result))))

(defn try-release []
  (try ((requiring-resolve 'borkdude.gh-release-artifact/overwrite-asset)
        {:org org
         :repo (name lib)
         :tag version
         :commit current-commit
         :file jar-file
         :content-type "application/java-archive"
         :draft false
         ;; vary the opts per attempt: gh-release-artifact memoizes release-for on its
         ;; opts map, so without this a failed lookup would be replayed on every retry
         :nonce (System/currentTimeMillis)})
       ;; catch Exception, not just ExceptionInfo: gh-release-artifact NPEs when the
       ;; GitHub list endpoint hasn't caught up with a just-created release (eventual
       ;; consistency); the backoff retry finds the release and uploads the asset.
       (catch Exception e
         (assoc (ex-data e) :failure? true :error (ex-message e)))))

(defn release [_]
  (jar nil)
  (println "Trying to release artifact...")
  (let [ret (retry-with-fib-backoff 10 try-release :failure?)]
    (if (:failure? ret)
      (do (println "GitHub release failed!")
          (System/exit 1))
      (println (:url ret)))))

(defn install [_]
  (clean nil)
  (jar nil)
  (b/install {:basis (b/create-basis {})
              :lib lib
              :version version
              :jar-file jar-file
              :class-dir class-dir}))
